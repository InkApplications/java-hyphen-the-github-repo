// configuration of the Maven artifacts
apply plugin: 'maven-publish'

// add javadoc/source jar tasks as artifacts
artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier "javadoc"
      }

      pom.withXml {
        def Node root = asNode()
        root.appendNode('name', project.project_name)
        root.appendNode('description', project.project_description)
        root.appendNode('url', project.project_url)

        def issues = root.appendNode('issueManagement')
        issues.appendNode('system', 'github')
        issues.appendNode('url', project.project_issues_url)

        def scm = root.appendNode('scm')
        scm.appendNode('url', project.project_scm)
        scm.appendNode('connection', project.project_connection)
        scm.appendNode('developerConnection', project.project_dev_connection)

        def license = root.appendNode('licenses').appendNode('license');
        license.appendNode('name', project.project_license)
        license.appendNode('url', project.project_license_url)
        license.appendNode('distribution', 'repo')

        def dev = root.appendNode('developers').appendNode('developer');
        dev.appendNode('id', project.project_vendor)
        dev.appendNode('name', project.project_vendor_name)
        dev.appendNode('organizationUrl', project.project_org_url)
      }
    }
  }
}
